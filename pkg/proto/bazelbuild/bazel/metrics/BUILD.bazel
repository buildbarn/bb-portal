"""
The .proto files have been downloaded from https://github.com/bazelbuild/bazel
using ./download.sh. The Bazel targets has then been manually crafted
to be able to keep the .proto files without modifications.
"""
# gazelle:ignore

load("@aspect_bazel_lib//lib:copy_file.bzl", "copy_file")
load("@aspect_bazel_lib//lib:write_source_files.bzl", "write_source_file")
load("@rules_go//go:def.bzl", "go_library")
load("@rules_go//proto:def.bzl", "go_proto_library")
load("@rules_proto//proto:defs.bzl", "proto_library")

[copy_file(
    name = name + "_proto_file",
    src = "@bazel_protos//protobuf:%s.proto" % name,
    out = name + ".proto",
    visibility = ["//visibility:public"],
) for name in [
    "package_load_metrics",
]]

proto_library(
    name = "package_load_metrics_proto",
    srcs = [":package_load_metrics_proto_file"],
    import_prefix = "src/main/java/com/google/devtools/build/lib/packages/metrics/",
    strip_import_prefix = "/pkg/proto/bazelbuild/bazel/metrics/",
    visibility = ["//visibility:public"],
    deps = [
        "@protobuf//:duration_proto",
    ],
)

go_proto_library(
    name = "package_load_metrics_go_proto",
    importpath = "github.com/bazelbuild/bazel/src/main/java/com/google/devtools/build/lib/packages/metrics",
    proto = ":package_load_metrics_proto",
    visibility = ["//visibility:public"],
)

go_library(
    name = "package_load_metrics",
    embed = [":package_load_metrics_go_proto"],
    visibility = ["//visibility:public"],
)

ALL_FILES = [
    "package_load_metrics",
]

[filegroup(
    name = s + "_go_proto_pb_go",
    srcs = [":" + s + "_go_proto"],
    output_group = "go_generated_srcs",
) for s in ALL_FILES]

[write_source_file(
    name = s + "_pb_go",
    in_file = ":" + s + "_go_proto_pb_go",
    out_file = s + ".pb.go",
    suggested_update_target = ":pb_go",
) for s in ALL_FILES]

write_source_file(
    name = "pb_go",
    additional_update_targets = [
        ":" + s + "_pb_go"
        for s in ALL_FILES
    ],
)
